config {
  type: 'incremental',
  description: 'Daily, weekly, and monthly aggregated metrics for product growth dashboard',
  schema: 'mart',
  tags: ['mart', 'product', 'growth', 'dashboard'],
  name: 'mart__product__main_dashboard__growth',
  bigquery: {
    partitionBy: 'calculation_date',
    clusterBy: ['last_active_city_id', 'last_active_warehouse_id']
  }
}

js {
  // Incremental refresh settings
  const LOOKBACK_DAYS_INCREMENTAL = 31;     // How many days to recalculate on incremental runs
  const FULL_REFRESH_START_DATE = '2020-01-01'; // Start date for full refresh
}

pre_operations {
  declare date_checkpoint date;
  set date_checkpoint = (
    ${when(incremental(),
      `select date_sub(max(calculation_date), interval ${LOOKBACK_DAYS_INCREMENTAL} day) from ${self()}`,
      `date('${FULL_REFRESH_START_DATE}')`
    )}
  );

  ${when(incremental(),
    `delete from ${self()} where calculation_date > date_checkpoint`
  )}
}

select
    "daily" as activty_type
  , calculation_date
  , last_active_city_id
  , last_active_warehouse_id
  , last_media_source
  , last_device_operating_system
  , count(distinct user_id) as all_users_cnt
  , count(distinct case when flag_daily = 1 then user_id else null end) as period_total_users_cnt
  , count(distinct case when flag_daily = 1 then user_id else null end) as active_users_cnt
  , count(distinct case when flag_daily = 1 then order_id else null end) as active_orders_cnt
  , count(distinct case when flag_daily = 1 and newbie_flag = 1 then user_id else null end) as active_newbie_users_cnt
  , count(distinct case when flag_daily = 1 and newbie_flag = 1 then order_id else null end) as active_newbie_orders_cnt
  , count(distinct case when flag_daily = 1 and newbie_flag = 0 then user_id else null end) as active_oldbie_users_cnt
  , count(distinct case when flag_daily = 1 and newbie_flag = 0 then order_id else null end) as active_oldbie_orders_cnt
  , count(distinct order_id) as period_total_orders_cnt
from ${ref('int__orders__moving_aggregation')}
where calculation_date > date_checkpoint
group by
    calculation_date
  , last_active_city_id
  , last_active_warehouse_id
  , last_media_source
  , last_device_operating_system
  , newbie_flag

union all

select
    "weekly" as activty_type
  , calculation_date
  , last_active_city_id
  , last_active_warehouse_id
  , last_media_source
  , last_device_operating_system
  , count(distinct user_id) as all_users_cnt
  , count(distinct case when flag_daily = 1 then user_id else null end) as period_total_users_cnt
  , count(distinct case when flag_7_days = 1 and flag_daily = 1 then user_id else null end) as active_users_cnt
  , count(distinct case when flag_7_days = 1 and flag_daily = 1 then order_id else null end) as active_orders_cnt
  , count(distinct case when flag_7_days = 1 and flag_daily = 1 and newbie_flag = 1 then user_id else null end) as active_newbie_users_cnt
  , count(distinct case when flag_7_days = 1 and flag_daily = 1 and newbie_flag = 1 then order_id else null end) as active_newbie_orders_cnt
  , count(distinct case when flag_7_days = 1 and flag_daily = 1 and newbie_flag = 0 then user_id else null end) as active_oldbie_users_cnt
  , count(distinct case when flag_7_days = 1 and flag_daily = 1 and newbie_flag = 0 then order_id else null end) as active_oldbie_orders_cnt
  , count(distinct order_id) as period_total_orders_cnt
from ${ref('int__orders__moving_aggregation')}
where calculation_date > date_checkpoint
group by
    calculation_date
  , last_active_city_id
  , last_active_warehouse_id
  , last_media_source
  , last_device_operating_system
  , newbie_flag

union all

select
    "monthly" as activty_type
  , calculation_date
  , last_active_city_id
  , last_active_warehouse_id
  , last_media_source
  , last_device_operating_system
  , count(distinct user_id) as all_users_cnt
  , count(distinct case when flag_daily = 1 then user_id else null end) as period_total_users_cnt
  , count(distinct case when flag_30_days = 1 and flag_daily = 1 then user_id else null end) as active_users_cnt
  , count(distinct case when flag_30_days = 1 and flag_daily = 1 then order_id else null end) as active_orders_cnt
  , count(distinct case when flag_30_days = 1 and flag_daily = 1 and newbie_flag = 1 then user_id else null end) as active_newbie_users_cnt
  , count(distinct case when flag_30_days = 1 and flag_daily = 1 and newbie_flag = 1 then order_id else null end) as active_newbie_orders_cnt
  , count(distinct case when flag_30_days = 1 and flag_daily = 1 and newbie_flag = 0 then user_id else null end) as active_oldbie_users_cnt
  , count(distinct case when flag_30_days = 1 and flag_daily = 1 and newbie_flag = 0 then order_id else null end) as active_oldbie_orders_cnt
  , count(distinct order_id) as period_total_orders_cnt
from ${ref('int__orders__moving_aggregation')}
where calculation_date > date_checkpoint
group by
    calculation_date
  , last_active_city_id
  , last_active_warehouse_id
  , last_media_source
  , last_device_operating_system
  , newbie_flag



