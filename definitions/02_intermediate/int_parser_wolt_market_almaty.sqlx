config {
  type: "incremental",
  uniqueKey: ["product_id"],
  schema: 'intermediate',
  name: 'int_parser_wolt_market_almaty',
  tags: ['parser'],
  bigquery: {
    partitionBy: "timestamp_trunc(pars_date_created_at, day)",
    clusterBy: ["pars_date_created_at"],
    updatePartitionFilter: 'pars_date_created_at >= timestamp_sub(current_timestamp(), interval 1 day)',
  },
  assertions: {
    nonNull: ["mercant_id", "product_id", "city"],
    rowConditions: [
      'PARSE_NUMERIC(price) > 0'
    ]
  },

}


pre_operations {
    DECLARE timestamp_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT timestamp(timestamp_sub(${dataform.projectConfig.vars.execution_date}, interval 1 day))`,
    `SELECT timestamp("2026-10-20")`)}
  );

  }


select
  JSON_EXTRACT_SCALAR(t.raw_data, '$.id') AS id,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.productId') AS product_id,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.title') AS title,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.description') AS description,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.url') AS url,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.image') AS url_picture,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.category') AS category_full_path,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.brand') AS brand,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.subCategory') AS sub_category,
  SAFE_CAST(JSON_EXTRACT_SCALAR(t.raw_data, '$.scrapedAt') AS TIMESTAMP) AS time_scrap,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.city') AS city,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.price') AS price,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.originalPrice') AS original_price,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.measure') AS measure,
  '70e6a58b-bf0a-4d53-b2b8-af4e0399047f' as mercant_id,
  'vlt-ala'as mercant_name, 
  created_at as pars_date_created_at,

from ${ref('stg_parser_wolt_market_almaty')} as t
    ${when(incremental(), "where TIMESTAMP_TRUNC(created_at, day) = timestamp_checkpoint")}
     


