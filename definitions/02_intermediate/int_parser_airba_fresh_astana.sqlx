config {
  type: "incremental",
  uniqueKey: ["SOURCE", "productId", "city"],
  schema: 'intermediate',
  name: 'int_parser_airba_fresh_astana',
  tags: ['parser'],
  bigquery: {
    partitionBy: "timestamp_trunc(pars_date_created_at, day)",
    clusterBy: ["mercant_id"],
    updatePartitionFilter: 'pars_date_created_at >= timestamp_sub(current_timestamp(), interval 1 day)',
  },
  assertions: {
    nonNull: ["id", "productId", "SOURCE"],
    rowConditions: [
      'PARSE_NUMERIC(price) > 0'
    ]
  },

}


pre_operations {
    DECLARE timestamp_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT timestamp(timestamp_sub(${dataform.projectConfig.vars.execution_date}, interval 1 day))`,
    `SELECT timestamp("2026-10-20")`)}
  );

  }


select
  JSON_EXTRACT_SCALAR(t.raw_data, '$.id') AS id,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.product_id') AS product_id,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.title') AS title,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.description') AS description,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.url') AS url,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.url_picture') AS url_picture,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.category_full_path') AS category_full_path,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.brand') AS brand,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.sub_category') AS sub_category,
  SAFE_CAST(JSON_EXTRACT_SCALAR(t.raw_data, '$.time_scrap') AS TIMESTAMP) AS time_scrap,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.city') AS city,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.price') AS price,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.originalPrice') AS originalPrice,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.discount') AS discount,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.currency') AS currency,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.inStock') AS inStock,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.weight') AS weight,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.volume') AS volume,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.reviewCount') AS reviewCount,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.productUrl') AS productUrl,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.productId') AS productId,
  SAFE_CAST(JSON_EXTRACT_SCALAR(t.raw_data, '$.parsedAt') AS TIMESTAMP) AS parsedAt,
  SAFE_CAST(JSON_EXTRACT_SCALAR(t.raw_data, '$.lastUpdated') AS TIMESTAMP) AS lastUpdated,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.source') AS SOURCE,
  JSON_EXTRACT_SCALAR(t.raw_data, '$.isActive') AS isActive,
  SAFE_CAST(JSON_EXTRACT_SCALAR(t.raw_data, '$.createdAt') AS TIMESTAMP) AS createdAt,
  SAFE_CAST(JSON_EXTRACT_SCALAR(t.raw_data, '$.updatedAt') AS TIMESTAMP) AS updatedAt,
  '7f67b894-86a9-4c2b-935d-123868b863cb' as mercant_id,
  'air-ast' as mercant_name,
  created_at as pars_date_created_at,

from ${ref('stg_parser_airba_fresh_astana')}  as t
    ${when(incremental(), "where TIMESTAMP_TRUNC(created_at, day) = timestamp_checkpoint")}
     


